// For building a single jar with everything in
plugins {
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

// Powered by Apache PDFBox and Commons CLI
apply plugin:'java'
repositories {
    mavenCentral()
}
dependencies {
    compile   group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.4'
    compile   group: 'commons-cli', name: 'commons-cli', version: '1.4'
}

// Build an all-in-one jar
shadowJar {
    def program = 'PDFtkBox'
    manifest {
      attributes 'Implementation-Title': 'PDFtkBox',
                 'Main-Class': 'com.quanticate.opensource.pdftkbox.'+program
    }
    classifier = ''
    baseName = program
    // Report what we built
    doLast {
       println " - Generated all-in-one jar ${program}.jar"
    }
}
// If not using the all-in-one, this is a bundle of dependencies
task zipDependencies(dependsOn: 'compileJava', type: Zip) {
    from configurations.compile.allArtifacts.files
    from configurations.compile
    archiveName project.name + "-dependencies.zip"
}

// Default is to build the all-in-one jar
defaultTasks 'shadowJar'
